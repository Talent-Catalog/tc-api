import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

// Gradle plugins required for the build
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.8.0'
    id 'com.google.cloud.tools.jib' version '3.4.4'
}

// Project metadata: Group and version details
group = 'org.tctalent'
version = '1.0.1-SNAPSHOT'

// Java toolchain configuration
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// Dependency configurations: Manage custom configurations
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// Repositories: Define where dependencies should be resolved from
repositories {
    // Use mavenLocal() for local iterative spec and service development
    // See: https://github.com/Talent-Catalog/tc-api/wiki/OpenAPI-Spec-Dependency
    //mavenLocal()
    mavenCentral()
}

// Project-wide variables: Manage dependency versions
ext {
    tcApiSpecVersion              = '1.0.0'
    mapstructVersion              = "1.6.3"
    lombokMapstructBindingVersion = "0.2.0"
    beanUtilsVersion              = "1.9.4"
    jsoupVersion                  = "1.15.4"
    jakartaAnnotationVersion      = "2.1.1"
    jjwtVersion                   = "0.11.2"
    swaggerVersion                = "2.2.8"
    jakartaValidationVersion      = "3.0.2"
    jsr305Version                 = "3.0.2"
}

// Project dependencies
dependencies {
    // Spring Boot starters
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "org.springframework.boot:spring-boot-starter-batch"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-mail"

    // TC open api spec
    implementation "org.tctalent:tc-api-spec:${tcApiSpecVersion}"

    // Database drivers and migration tools
    runtimeOnly "org.postgresql:postgresql"
    //Note that Spring Initializr pulls in org.flywaydb:flyway-core but that will not process
    //older Postgres DB versions. We need the the following dependency
    //See https://github.com/flyway/flyway/issues/3902
    implementation 'org.flywaydb:flyway-database-postgresql'

    // Lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // Testing dependencies
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation 'org.springframework.security:spring-security-test'

    // Additional libraries required by imported TC entities
    implementation "org.apache.commons:commons-lang3"
    implementation ("commons-beanutils:commons-beanutils:${beanUtilsVersion}") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation "org.jsoup:jsoup:${jsoupVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"
    implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"

    // For OpenAPI 3.0 code generation
    implementation "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
    implementation "io.swagger.core.v3:swagger-models:${swaggerVersion}"
    implementation "jakarta.validation:jakarta.validation-api:${jakartaValidationVersion}"

    // Java 8 date/time handling with Jackson
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "com.google.code.findbugs:jsr305:${jsr305Version}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
}

// Include generated sources in the main source set
sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "$buildDir/generated/sources/annotationProcessor/java/main"]
        }
    }
}

// Testing configuration: Use JUnit Platform
tasks.named('test') {
    useJUnitPlatform()
}

// Java compilation options
compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

// Additional compiler options
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            "-Amapstruct.defaultComponentModel=spring" // MapStruct integration with Spring
    ]
}

// Task-specific configuration: Force task outputs to always execute
tasks.withType(GenerateTask).configureEach {
    outputs.upToDateWhen { false }
    outputs.cacheIf { false }
}

jib {
    to {
        if (project.hasProperty("test-tc-system")) {
            image = "231168606641.dkr.ecr.us-east-1.amazonaws.com/tc-api"
        } else if (project.hasProperty("prod-tc-system")) {
            image = "968457613372.dkr.ecr.us-east-1.amazonaws.com/tc-api"
        }
        tags = ["latest", version]
        credHelper = 'ecr-login'
    }
}
